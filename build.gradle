plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.8.10"
    id "org.jetbrains.dokka" version "1.7.20"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

if (hasProperty("buildScan")) {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
    }
}

static def determineVersion(String defaultVersion) {
    def envVersion = System.getenv("VERSION")
    return envVersion == null ? defaultVersion : envVersion
}

group = "dev.mikchan.mcnp"
version = determineVersion("2.1.4")

repositories {
    mavenCentral()

    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }

    maven {
        name = "placeholderapi"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }

    maven {
        name = "papermc"
        url = "https://repo.papermc.io/repository/maven-public/"
    }

    maven {
        name = "Scarsz-Nexus"
        url = "https://nexus.scarsz.me/content/groups/public/"
    }
}

dependencies {
    //noinspection VulnerableLibrariesLocal
    compileOnly "org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT"

    compileOnly "me.clip:placeholderapi:2.10.9"
    compileOnly "com.discordsrv:discordsrv:1.26.3-SNAPSHOT"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.10"
    implementation "dev.dejvokep:boosted-yaml-spigot:1.4"

    shadow "org.bstats:bstats-bukkit:3.0.1"
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }

    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dokkaHtml {
    outputDirectory.set(file("${buildDir}/dokka"))
}

javadocJar {
    archiveClassifier.set("javadoc")
    dependsOn("dokkaHtml")
    from("$buildDir/dokka")
}

shadowJar {
    archiveClassifier.set(null)
    configurations = [project.configurations.shadow]
    minimize()
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "dev.mikchan.mcnp.chat.shadow"
}
tasks.shadowJar.dependsOn tasks.relocateShadowJar

publishing {
    repositories {
        maven {
            name = "GitHub"
            url = "https://maven.pkg.github.com/MikChanNoPlugins/Chat"
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            groupId = group
            artifactId = "chat"

            artifact jar
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

if (file("local.gradle").exists()) {
    apply from: "local.gradle"
}
